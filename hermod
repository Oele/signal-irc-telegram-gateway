#!/usr/bin/perl
#
# IRC Bot for Hermod Telegram Gateway. Reads from a logfile to be send to irc and relays
# messages from irc to telegram and to another logfile which is send to a signal group
# by signalpoller.
#
# 2019, Ruben de Groot

use strict;
use warnings;
use POE qw(Component::IRC Component::IRC::Plugin::FollowTail Component::SSLify);
use HTTP::Tiny;
use URI::Escape;
use JSON qw( decode_json );

my $cfg = "/etc/hermod.json";
open my $fh, '<', $cfg or die "error opening $cfg $!";
my $config = decode_json do { local $/; <$fh> };

my $ircname  = 'Hermod Gateway';
my $URL = "https://api.telegram.org/bot$config->{telegram}->{token}/sendMessage";

# We create a new PoCo-IRC object
my $irc = POE::Component::IRC->spawn(
   nick    => $config->{irc}->{nick},
   ircname => $ircname,
   server  => $config->{irc}->{node},
   Port    => $config->{irc}->{port},
   UseSSL  => $config->{irc}->{UseSSL},
) or die "Oh noooo! $!";

POE::Session->create(
    package_states => [
        main => [ qw(_default _start irc_001 irc_public irc_tail_input ) ],
    ],
    heap => { irc => $irc },
);

$poe_kernel->run();

sub _start {
    # file to be read and send to the channel
    $irc->plugin_add( 'FollowTail' => POE::Component::IRC::Plugin::FollowTail->new(
        filename => $config->{irc}->{infile},
    ));
    $irc->yield( register => 'all' );
    $irc->yield( connect => { } );
    return;
}

sub irc_001 {
    my $sender = $_[SENDER];
    my $irc = $sender->get_heap();
    print "Connected to ", $irc->server_name(), "\n";

    # we join our channel
    $irc->yield( join => $config->{irc}->{channel} );
    return;
}

sub irc_public {
    my ($sender, $who, $where, $what) = @_[SENDER, ARG0 .. ARG2];
    my $nick = ( split /!/, $who )[0];

    # we relay all messages straight to telegram
    my $text = uri_escape_utf8("[IRC] $nick: $what");
    my $data = "chat_id=$config->{telegram}->{chat_id}&text=$text";
    HTTP::Tiny->new->get( "$URL?$data" );

    # we write to the signal infile
    open my $w, ">>:utf8", $config->{signal}->{infile};
    print $w "[IRC] $nick: $what\n";
    close $w;

    return;
}

sub irc_tail_input {
    my ($kernel, $sender, $filename, $input) = @_[KERNEL, SENDER, ARG0, ARG1];
    $irc->yield( privmsg => $config->{irc}->{channel} => $input );
    return;
}

# This will produce some debug info.
sub _default {
    my ($event, $args) = @_[ARG0 .. $#_];
    my @output = ( "$event: " );

    for my $arg (@$args) {
        if ( ref $arg eq 'ARRAY' ) {
            push( @output, '[' . join(', ', @$arg ) . ']' );
        }
        else {
            push ( @output, "'$arg'" );
        }
    }
    print join ' ', @output, "\n";
    die "I got disconnected :( :(\nKilling myself..\n" if grep(/irc_disconnect/,@output);
    return;
}
