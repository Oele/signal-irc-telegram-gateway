#!/usr/bin/perl -w
#
# Signal Poller for Hermod Gateway Bot.
#
# Polling the signal group for new messages. These are send to telegram 
# and irc
#
# 2019-08-23, Ruben de Groot

use strict;
use threads;
use JSON;
use HTTP::Tiny;
use URI::Escape;
use Text::Unidecode;
use DBI;
use Data::Dumper;

my $cfg = "/etc/hermod.json";
open my $fh, '<', $cfg or die "error opening $cfg $!";
my $config = decode_json do { local $/; <$fh> };
$config->{nicks} = getalias($config);

print Dumper($config);

# start a seperate thread to send messages to the signal group
threads->create(\&sender,$config);

my $json = JSON->new->allow_nonref;
while(1) {
    my @lines = qx( $config->{signal_cli} -u $config->{signal_phone} receive --json );
    for my $line (@lines) {
        print $line; chomp $line;
        my $sigmsg = $json->decode($line);
        my $sender = (defined $sigmsg->{envelope}->{source}) ? $sigmsg->{envelope}->{source} : "";
        my $group = (defined $sigmsg->{envelope}->{dataMessage}->{groupInfo}->{groupId}) ? $sigmsg->{envelope}->{dataMessage}->{groupInfo}->{groupId} : "";
        my $text = (defined $sigmsg->{envelope}->{dataMessage}->{message}) ? $sigmsg->{envelope}->{dataMessage}->{message} : "";
        relay($config,$sender,$text) if $sender and $text and $group eq $config->{signal_gid};
    }
}

sub relay {
    my ($config,$sender,$text) = @_;
    my $anonsender = $sender;

    # anonymize sender telephone
    if (defined $config->{nicks}->{$sender}) {
        $anonsender = $config->{nicks}->{$sender};
    } else {
        $anonsender =~ s/.*(\d\d\d\d)$/Piraat-$1/;
    }

    # check for setnick command
    if ($text =~ /^!setnick/) {
        (my $nick = $text) =~ s/^!setnick ([\w]+).*$/$1/;
        if ($nick) {
            setalias($sender,$nick);
            $config->{nicks}->{$sender} = $nick;
            $text = "$anonsender is now known as $nick\n";

            # notify all chats
            open my $w, ">>", $config->{tosignal} or die "Died opening tosignal $config->{tosignal}";
            print $w "$text";
            close $w;

            open my $w, ">>", $config->{sigfile} or die "Died opening sigfile $config->{sigfile}";
            print $w "[Sg] $text";
            close $w;

            my $URL = "https://api.telegram.org/bot$config->{token}/sendMessage?chat_id=$config->{chat_id}&text=";
            my $telmsg = uri_escape("[Sg] $text");
            my $response = HTTP::Tiny->new->get( "$URL$telmsg" );
            print "$response->{status} $response->{reason}\n";

            return;
        }
    }

    # relay to telegram
    my $URL = "https://api.telegram.org/bot$config->{token}/sendMessage?chat_id=$config->{chat_id}&text=";
    my $telmsg = uri_escape_utf8("[Sg $anonsender]: $text");
    my $response = HTTP::Tiny->new->get( "$URL$telmsg" );
    print "$response->{status} $response->{reason}\n";

    # relay to IRC
    $text = unidecode($text);
    open my $w, ">>", $config->{sigfile} or die "Died opening sigfile $config->{sigfile}";
    print $w "[Sg $anonsender]: $text\n";

    return;
}

sub sender {
    my $config = shift;
    my $inode = (stat($config->{tosignal}))[1];
    open(LFILE,$config->{tosignal}) or die @_;

    # SEEK_END
    seek(LFILE, 0, 2) or die @_;

    for (;;) {
        sleep 5;
        # check if logfile hasn't turned over below our feet
        if ($inode != (stat($config->{tosignal}))[1]) {
            close(LFILE);
            $inode = (stat($config->{tosignal}))[1];
            open(LFILE,$config->{tosignal}) or next;
        } else {
            # SEEK_CUR
            seek(LFILE, 0, 1);
        }
        my $lines = join "\n", (<LFILE>);
        qx( $config->{signal_cli} -u $config->{signal_phone} send -g $config->{signal_gid} -m "$lines" ) if $lines;
    }
}

sub getalias {
    my $config = shift;
    my $alias;
    my $dbh = DBI->connect("dbi:SQLite:dbname=$config->{signal_db}", "", "", { RaiseError => 1 }, ) or die $DBI::errstr;
    my $obj = $dbh->prepare("select * from alias");
    $obj->execute() or die $DBI::errstr;

    while (my $row = $obj->fetchrow_hashref()) {
        $alias->{$row->{phone}} = $row->{nick};
    }
    return $alias;
}

sub setalias {
    my ($phone,$nick) = @_;
    my $dbh = DBI->connect("dbi:SQLite:dbname=$config->{signal_db}", "", "", { RaiseError => 1 }, ) or die $DBI::errstr;
    $dbh->do("insert or replace into alias (phone,nick) values (?,?)", undef, $phone, $nick) or die $DBI::errstr;
    $dbh->disconnect();
}
