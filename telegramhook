#!/usr/bin/perl -w
#
# Webhook for Hermod Telegram Gateway Bot. The hook can be set by the command:
#
# curl -F "url=https://webserver/cgi-bin/telehook" https://api.telegram.org/bot$TOKEN/setWebhook
#
# messages are filtered a little and then written
# to a logfile, for processing by the bot
#
# 2019, Ruben de Groot

use strict;
use CGI::Fast qw/:standard/;
use JSON;
use TOML;
use Text::Unidecode;
use DBI;

open my $fh, '<', "/etc/hermod.toml" or die "error opening configuration $!";
my ($cfg, $e) = from_toml do { local $/; <$fh> };
unless ($cfg) {
    die "Error parsing toml: $e";
}

# communicate with bot through logfiles
open my $irc, '>>:utf8', $cfg->{irc}->{infile} if defined $cfg->{irc}->{infile};
open my $sig, '>>:utf8', $cfg->{signal}->{infile} if defined $cfg->{signal}->{infile};
open my $mat, '>>:utf8', $cfg->{matrix}->{infile} if defined $cfg->{matrix}->{infile};
open my $wha, '>>:utf8', $cfg->{whatsapp}->{infile} if defined $cfg->{whatsapp}->{infile};
open my $dbg, '>>:utf8', $cfg->{telegram}->{debug} if defined $cfg->{telegram}->{debug};

unless (defined $cfg->{telegram}->{token} and defined $cfg->{telegram}->{chat_id}) {
    print CGI->header(-type => 'application/json');
    print '{"ok": false, "status": 500, "error": "token or chat_id undefined"}'."\n\n";
    exit;
}
my $tel = $cfg->{telegram};

# bots cannot get a list of group members, so we keep a database
my $dbh = DBI->connect("dbi:SQLite:dbname=$tel->{db}", "", "", { RaiseError => 1 }, )
        if defined $tel->{db};
my $st_ins = $dbh->prepare("insert into teleusers (id,first_name,last_name,username,is_bot) values (?,?,?,?,?)")
        if defined $dbh;
my $st_del = $dbh->prepare("delete from teleusers where id = ?")
        if defined $dbh;

while (my $cgi = CGI::Fast->new) {
    my $body = (defined $cgi->param('POSTDATA')) ? $cgi->param('POSTDATA') : '{}';
    my $dj = decode_json( $body );
    
    print $dbg "$body\n" if defined $dbg;
    
    # check type of msg and chat_id
    if (defined $dj->{message}->{chat}->{id} and $dj->{message}->{chat}->{id} == $tel->{chat_id}) {

        my $msg = $dj->{message};
        my $user_id = (defined $msg->{from}->{id} and $msg->{from}->{id} =~ /^\d+$/) ?
            $dj->{'message'}->{'from'}->{id} : 0;
        my $text = (defined $msg->{text}) ? $msg->{text} : "";
        my $first = (defined $msg->{from}->{first_name}) ? $msg->{from}->{first_name} : "";
        my $last = (defined $msg->{from}->{last_name}) ? $msg->{from}->{last_name} : "";
        my $caption = (defined $msg->{caption}) ? $msg->{caption} : "";
        
        # look for photo's and documents
        my ($type,$doc);
        if (defined $msg->{photo}) {
            $doc = @{$msg->{photo}}[-1];
            $type = 'photo';
        }
        foreach my $t ('voice', 'audio', 'document', 'video') { 
            if (defined $msg->{$t}) {
                $doc = $msg->{$t};
                $type = $t;
            }
        }
        
        if (defined $doc->{file_id}) {

            # download the file
            my $response = qx( curl -s "https://api.telegram.org/bot$tel->{token}/getFile?file_id=$doc->{file_id}" );
            eval {
                my $djfile = decode_json( $response );
                my $path = $djfile->{result}->{file_path};
                (my $file = $path) =~ s#.*/##;
                my $name = (defined $doc->{file_name}) ? $doc->{file_name} : $file;
                qx( wget -O $tel->{attachments}/$file https://api.telegram.org/file/bot$tel->{token}/$path >/dev/null 2>&1 );
                if ($? == 0) {
                    print $sig "FILE:$tel->{attachments}/$file [sent by $first $last on telegram] $caption\n" if defined $sig;
                    print $mat "FILE:$tel->{attachments}/$file [sent by $first $last on telegram] $caption\n" if defined $mat;
                    print $wha "FILE:$doc->{mime_type}:$tel->{attachments}/$file [sent by $first $last on telegram] $caption\n" if defined $wha;
                    $text = "** sends $type: $tel->{url}/$file";
                    $text .= " with caption: $caption" if $caption;
                } else {
                    print $dbg "Error getting $type $name $file\n" if defined $dbg;
                }
            }
        }
        
        # is the message a sticker?
        if (defined $msg->{sticker}) {
            my $sticknam = (defined $msg->{sticker}->{set_name}) ? $msg->{sticker}->{set_name} : '';
            $text .= "\nsends a $sticknam sticker ";
            $text .= $msg->{sticker}->{emoji} if defined $msg->{sticker}->{emoji};
        }
        
        # is the message a reply?
        $text = "(reply to: $msg->{reply_to_message}->{text}) " . $text if defined $msg->{reply_to_message} and
            $msg->{reply_to_message}->{text};

        # is the message a forward?
        if (defined $msg->{forward_from_chat}->{id} or defined $msg->{forward_from}->{id}) {
            my $forw_id = (defined $msg->{forward_from}->{id}) ? $msg->{forward_from}->{id} :
                $msg->{forward_from_chat}->{id};

            my $title = (defined $msg->{forward_from}->{title}) ? $msg->{forward_from}->{title} :
                (defined $msg->{forward_from_chat}->{title}) ? $msg->{forward_from_chat}->{title} :
                (defined $msg->{forward_from}->{first_name}) ? $msg->{forward_from}->{first_name} :
                (defined $msg->{forward_from_chat}->{first_name}) ? $msg->{forward_from_chat}->{first_name} : "Forward";
            $title = $msg->{forward_from_chat}->{first_name} unless $title;

            $text = "[Fwd from $title]: $msg->{text}\n";
            print $irc $text if defined $irc;
            print $sig $text if defined $sig;
            print $mat $text if defined $mat;
            print $wha $text if defined $wha;
            ok("forwarded msg: $msg->{text} from $title\n");
        }

        # is the message a channel post?
        
        # look for new chat members
        if (defined $msg->{new_chat_members}) {
            my $members = '';
            foreach my $m (@{$msg->{new_chat_members}}) {
                $st_ins->execute($m->{id},$m->{first_name},$m->{last_name},$m->{username},$m->{is_bot}) if defined $st_ins;
                $members .= ', ' if $members;
                $members .= "$m->{first_name}";
                $members .= " $m->{last_name}" if $m->{last_name};
            }
            $text = "[tel] $members joined the chat\n";
            print $irc $text if defined $irc;
            print $sig $text if defined $sig;
            print $mat $text if defined $mat;
            print $wha $text if defined $wha;
            ok("New member(s): $members\n");
        }
        
        # look for leaving chat members
        if (defined $msg->{left_chat_member}->{id}) {
            $st_del->execute($msg->{left_chat_member}->{id}) if defined $st_del;
            $text = "[tel] $msg->{left_chat_member}->{first_name} left the chat\n";
            print $irc $text if defined $irc;
            print $sig $text if defined $sig;
            print $mat $text if defined $mat;
            print $wha $text if defined $wha;
            ok("Leaving member: $msg->{left_chat_member}->{first_name}\n");
        }

        # look for commands
        if ($text =~ /!users/) {

            # user list asked; relay command to hermod
            print $irc "CMD!TEL!users\n" if defined $irc;

            ok("!users command relayed for $first $last\n");
        }

        # split lines and write to log for irc, matrix
        my @lines = split /\n/, $text;
        for my $msg (@lines) {
            next unless $msg;

            # send to IRC, split lines in chunks of ~maxmsg size if necessary
            if (defined $irc and length $msg > $cfg->{irc}->{maxmsg}) {
                $msg =~ s/(.{1,$cfg->{irc}->{maxmsg}}\S|\S+)\s+/$1\n/g;
                print $irc "[tel] $first $last: $_\n" for split /\n/, $msg;
            } elsif (defined $irc) {
                print $irc "[tel] $first $last: $msg\n";
            }

        }

        # send to matrix, whatsapp
        print $mat "[tel] $first $last: $text\n" if defined $mat;
        print $wha "[tel] $first $last: $text\n" if defined $wha;

        # for signal, skip documents, photo's. they've been sent already.
        print $sig "[tel] $first $last: $text\n" unless $text =~ /\*\* sends / or not defined $sig;
    }
    ok();
}

sub ok {
    my $msg = shift;
    print $dbg $msg if defined $msg and defined $dbg;
    print CGI->header(-type => 'application/json');
    print '{"ok": true, "status": 200}'."\n\n";
    exit;
}

