#!/usr/bin/perl -w
#
# Webhook for Hermod Gateway Bot. The hook can be set by the command:
#
# curl -F "url=https://webserver/cgi-bin/telehook" https://api.telegram.org/bot$TOKEN/setWebhook
#
# messages are filtered a little and then written
# to a unix domain socket, for processing by the bot
#
# 2019-08-17, Ruben de Groot

use strict;
use CGI qw/:standard/;
use JSON;
use IO::Socket::UNIX;
use Data::Dumper;

my $config = do "/etc/hermod.cfg";
die "No usable config found!\nHint: look at hermod.cfg.example\n" unless ref $config eq "HASH";

my $socket = $config->{socket};
my $group = $config->{group};
my $chat_id = $config->{chat_id};
my $ok = '{"ok":true,"result":true}';

# communicate with bot through its POE::Component::IRC::Plugin::FollowTail plugin
open my $log, '>>', "/var/www/log/telegramhook.log";
open my $dbg, '>>', "/var/www/log/telegramhook.debug";

my $cgi    = CGI->new;
my $body = (defined $cgi->param('POSTDATA')) ? $cgi->param('POSTDATA') : '{}';
my $dj = decode_json( $body );

# check type of msg and chat_id
exit unless defined $dj->{'message'}->{'chat'}->{'id'};
exit unless $dj->{'message'}->{'chat'}->{'id'} == $chat_id;

my $user_id = (defined $dj->{'message'}->{'from'}->{'id'} and $dj->{'message'}->{'from'}->{'id'} =~ /^\d+$/) ?
    $dj->{'message'}->{'from'}->{'id'} : 0;
my $text = (defined $dj->{'message'}->{'text'}) ? $dj->{'message'}->{'text'} : "";
my $first = (defined $dj->{'message'}->{'from'}->{'first_name'}) ? $dj->{'message'}->{'from'}->{'first_name'} : "";
my $last = (defined $dj->{'message'}->{'from'}->{'last_name'}) ? $dj->{'message'}->{'from'}->{'last_name'} : "";

# substitute newlines
my @lines = split /\n/, $text;

print $dbg $body."\n";
print $log "Msg by $first $last: $_\n" for @lines;

print $cgi->header(-type => 'application/json', -connection => 'close');
print $ok."\n\n";

