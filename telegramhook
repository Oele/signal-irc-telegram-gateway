#!/usr/bin/perl -w
#
# Webhook for Hermod Telegram Gateway Bot. The hook can be set by the command:
#
# curl -F "url=https://webserver/cgi-bin/telehook" https://api.telegram.org/bot$TOKEN/setWebhook
#
# messages are filtered a little and then written
# to a unix domain socket, for processing by the bot
#
# 2019-08-17, Ruben de Groot

use strict;
use CGI qw/:standard/;
use JSON;
use IO::Socket::UNIX;
use Data::Dumper;
use utf8;
use Text::Unidecode;

my $cfg = "/etc/hermod.json";
open my $fh, '<', $cfg or die "error opening $cfg $!";
my $config = decode_json do { local $/; <$fh> };
die "No usable config found!\nHint: look at hermod.json.example\n" unless ref $config eq "HASH";

(my $dbgfile  = $config->{logfile}) =~ s/\.log$/.debug/;
my $ok = '{"ok":true,"result":true}';

# communicate with bot through its POE::Component::IRC::Plugin::FollowTail plugin
open my $log, '>>:utf8', $config->{logfile};
open my $dbg, '>>', $dbgfile;

my $cgi    = CGI->new;
my $body = (defined $cgi->param('POSTDATA')) ? $cgi->param('POSTDATA') : '{}';
my $dj = decode_json( $body );

print $dbg $body."\n";

# check type of msg and chat_id
exit unless defined $dj->{'message'}->{'chat'}->{'id'};
exit unless $dj->{'message'}->{'chat'}->{'id'} == $config->{chat_id};

my $user_id = (defined $dj->{'message'}->{'from'}->{'id'} and $dj->{'message'}->{'from'}->{'id'} =~ /^\d+$/) ?
    $dj->{'message'}->{'from'}->{'id'} : 0;
my $text = (defined $dj->{'message'}->{'text'}) ? unidecode($dj->{'message'}->{'text'}) : "";
my $first = (defined $dj->{'message'}->{'from'}->{'first_name'}) ? unidecode($dj->{'message'}->{'from'}->{'first_name'}) : "";
my $last = (defined $dj->{'message'}->{'from'}->{'last_name'}) ? unidecode($dj->{'message'}->{'from'}->{'last_name'}) : "";

# split lines
my @lines = split /\n/, $text;

print $log "$first $last: $_\n" for @lines;

print $cgi->header(-type => 'application/json', -connection => 'close');
print $ok."\n\n";

