#!/usr/bin/perl -w
#
# Webhook for Hermod Telegram Gateway Bot. The hook can be set by the command:
#
# curl -F "url=https://webserver/cgi-bin/telehook" https://api.telegram.org/bot$TOKEN/setWebhook
#
# messages are filtered a little and then written
# to a logfile, for processing by the bot
#
# 2019-08-17, Ruben de Groot

use strict;
use CGI qw/:standard/;
use JSON;
use Text::Unidecode;

my $cfg = "/etc/hermod.json";
open my $fh, '<', $cfg or die "error opening $cfg $!";
my $config = decode_json do { local $/; <$fh> };
die "No usable config found!\nHint: look at hermod.json.example\n" unless ref $config eq "HASH";

my $ok = '{"ok":true,"result":true}';

# communicate with bot through a logfile
open my $irc, '>>:utf8', $config->{telfile};
open my $sig, '>>', $config->{tosignal};
open my $dbg, '>>', $config->{telegram_debug} if defined $config->{telegram_debug};

my $cgi    = CGI->new;
my $body = (defined $cgi->param('POSTDATA')) ? $cgi->param('POSTDATA') : '{}';
my $dj = decode_json( $body );

print $dbg "$body\n" if defined $config->{telegram_debug};

# check type of msg and chat_id
finish("no chat id\n") unless defined $dj->{'message'}->{'chat'}->{'id'};
finish("wrong chat id\n") unless $dj->{'message'}->{'chat'}->{'id'} == $config->{chat_id};

my $user_id = (defined $dj->{'message'}->{'from'}->{'id'} and $dj->{'message'}->{'from'}->{'id'} =~ /^\d+$/) ?
    $dj->{'message'}->{'from'}->{'id'} : 0;
my $text = (defined $dj->{'message'}->{'text'}) ? $dj->{'message'}->{'text'} : "";
my $first = (defined $dj->{'message'}->{'from'}->{'first_name'}) ? $dj->{'message'}->{'from'}->{'first_name'} : "";
my $last = (defined $dj->{'message'}->{'from'}->{'last_name'}) ? $dj->{'message'}->{'from'}->{'last_name'} : "";

$text .= "\nsends a photo" if defined $dj->{'message'}->{'photo'};
$text .= "\nsends a sticker" if defined $dj->{'message'}->{'sticker'};

# split lines and write to logs for signal and irc
my @lines = split /\n/, $text;
foreach my $line (@lines) {
    print $sig "[Tg $first $last]: $line\n" if $line;
    print $irc unidecode("[Tg $first $last]: $line\n");
}

finish("Message relayed\n");

sub finish {
    print $dbg shift if defined $config->{telegram_debug};
    print $cgi->header(-type => 'application/json', -connection => 'close');
    print $ok."\n\n";
    exit();
}


